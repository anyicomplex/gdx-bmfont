allprojects {
    apply plugin: "java"

    group 'io.github.anyicomplex'
    version '1.0.0'

    repositories {
        mavenCentral()
    }
    ext {
        gdxVersion = '1.10.0'
        picocliVersion = '4.6.2'
    }
    dependencies {
        implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    }

    test {
        useJUnitPlatform()
    }

}

subprojects {
    dependencies {
        implementation project(":")
    }
}

project(":lwjgl3:cli") {
    dependencies {
        implementation "info.picocli:picocli:$picocliVersion"
        annotationProcessor "info.picocli:picocli-codegen:$picocliVersion"
    }
}

project(":lwjgl3") {
    subprojects {
        dependencies {
            implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
            implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
            implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
        }
        ext.mainClassName = "com.anyicomplex.gdx.tools.bmfont.GdxBMFont"
        jar {
            duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
            dependsOn configurations.runtimeClasspath
            from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
            exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
            dependencies {
                exclude('META-INF/INDEX.LIST', 'META-INF/maven/**')
            }
            manifest {
                attributes 'Main-Class': project.mainClassName
            }
            doLast {
                file(archiveFile).setExecutable(true, false)
            }
        }
    }
}
